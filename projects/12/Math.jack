// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
		static int diff;
		static Array twoToPower;
        /** Initializes the library. */
        function void init() {
			var int j,i;
			let j = 1;
			let i = 0;
			let twoToPower = Array.new(16);
			while(i<16){
				let twoToPower[i] = j;
				let j = ^j;
				let i = i+1;
			}
			return;
        }

        /** Returns the absolute value of x. */
        function int abs(int x) {
			if (x > 0) {
				return x;
			}
			else {
				return -x;
			}
        }

        /** 
         * Returns the product of x and y. 
         * When a Jack compiler detects the multiplication operator '*' in the 
         * program's code, it handles it by invoking this method. In other words,
         * the Jack expressions x*y and multiply(x,y) return the same value.
         */
        function int multiply(int x, int y) {
            // Pseudocode:
            // 1. let sum = 0
            // 2. let shiftedx = x
            // 3. for i = 0 ... n-1 do
            // 4.   if ((i-th bit of y) == 1)
            // 5.      let sum = sum + shiftedx
            // 6.   let shiftedx = 2*shiftedx
            // 7. return sum
			
			var int sum, i, mask;
			let i = 0;
			let sum = 0;
			let mask = 1;
			while(i < 16){
				if(~((mask&y) = 0)){
					let sum = sum + x;
				}
				let x = ^x;
				let mask = ^mask;
				let i = i+1;
			}
			return sum;
        }

        /** 
         * Returns the integer part of x/y.
         * When a Jack compiler detects the multiplication operator '/' in the 
         * program's code, it handles it by invoking this method. In other words,
         * the Jack expressions x/y and divide(x,y) return the same value.
         */
		 
        function int divide(int x, int y) {
            // This should be implemented without using multiplication.
            // Hint: updating the value of 2*q*y in the "if" statement requires
            // at most a single addition operator at every recursion step.
            // Pseudocode:
            // 1. if (y > x) return 0
            // 2. let q = divide(x, 2*y)
            // 3. if ((x - 2*q*y) < y)
            // 4.   return 2*q
            // 5. else
            // 6.   return 2*q + 1
			var int q, countNeg, sign, result;
			
			
			//handle negative numbers
			let countNeg = 0;
			if(x < 0){
				let countNeg = countNeg + 1;
				let x = 0-x;
			}
			if(y < 0){
				let countNeg = countNeg + 1;
				let y = 0-y;
			}
			let sign = 1;
			if (countNeg = 1){
				let sign = -1;
			}
			//division algorithem
			if (y > x){
				let diff = 0;
				return 0;
			}
			let q = x / (^y);
			if((x-diff) < y){
				let result = ^q;
			}
			else {
				let diff = diff + y;
				let result = ^q+1;
			}
			if(sign < 0){
				let result = -result;
			}
			
			return result;
		}

        /** Returns the integer part of the square root of x. */
        function int sqrt(int x) {
            // This should be implemented without using multiplication or division.
            // Pseudocode (where '**' is the mathematical power function):
            // 1. let y = 0
            // 2. for j = (n/2 - 1) ... 0 do
            // 3.   if ((y + 2**j)**2 <= x) then let y = y + 2**j
            // 4. return y
			var int j,y,twoToJ,nextSquared,prevSquared,y2j;
			let y = 0;
			let j = 7;		
			
			let prevSquared = 0;
			let y2j = 0;
			while(j > -1){
				let nextSquared = prevSquared + ^y2j + twoToPower[^j];
				if ((~(nextSquared > x)) & (nextSquared> 0)){
					let prevSquared = nextSquared;
					let y = y + twoToPower[j];
					let y2j = y2j + twoToPower[^j];
				}
				let y2j = #y2j;
				let j = j-1;
			}		
			return y;
        }

        /** Returns the greater number. */
        function int max(int a, int b) {
			if(a > b){
				return a;
			}
			return b;
        }

        /** Returns the smaller number. */
        function int min(int a, int b) {
			if(a>b){
				return b;
			}
			return a;
        }
}
